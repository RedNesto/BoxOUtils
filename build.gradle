buildscript {
    ext.kotlin_version = '1.3.40'
    ext.shadow_version = '5.0.0'
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'maven-publish'

    id 'org.spongepowered.plugin' version '0.9.0'
    id 'net.minecrell.licenser' version '0.4.1'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'

allprojects {
    apply plugin: 'java'
    apply plugin: 'net.minecrell.licenser'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spongepowered.org/maven/'
        }
        maven {
            url 'https://jitpack.io'
        }
    }

    license {
        header = rootProject.file('LICENSE')

        include '**/*.java', '**/*.kt'

        newLine = false
    }
}

archivesBaseName = 'BoxOUtils'

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeApiVersion}"
    annotationProcessor "org.spongepowered:spongeapi:${spongeApiVersion}"

    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

    testCompile "org.spongepowered:spongeapi:${spongeApiVersion}"

    gradle.ext.alwaysIncludedIntegrations.each { testCompile(project("integrations:$it")) }

    testRuntime 'org.slf4j:slf4j-simple:1.7.25'
}

sponge.plugin.id = 'box-o-utils'

build.dependsOn shadowJar
shadowJar {
    archiveClassifier.set(null)
    withIntegrations { proj -> from proj.jar.outputs.files }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
    systemProperty 'bou.is_testing', 'true'
}

task('sourcesJar', type: Jar) {
    dependsOn 'classes'
    archiveClassifier.set('sources')
    afterEvaluate {
        from sourceSets['main'].allSource
        withIntegrations { proj -> from proj.sourceSets['main'].allSource }
    }
}

task('javadocJar', type: Jar) {
    dependsOn 'javadoc'
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
    withIntegrations { proj -> from proj.javadoc.destinationDir }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = 'box-o-utils'
            version = version

            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

def withIntegrations(Action<Project> action) {
    try {
        for (path in project('integrations').subprojects) {
            action.execute(path)
        }
    } catch (UnknownProjectException ignore) {
    }
}

if (project.file('local.gradle').exists()) {
    // in case anyone wants to have additional tasks
    apply from: 'local.gradle'
}
