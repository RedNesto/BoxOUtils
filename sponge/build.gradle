plugins {
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'

    id 'org.spongepowered.plugin' version '0.9.0'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'com.qixalite.spongestart2' version '4.0.0'
}

archivesBaseName = 'BoxOUtils'

repositories {
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'http://files.minecraftforge.net/maven'
    }
}

dependencies {
    compile project(':common')

    compileOnly 'org.spongepowered:spongeapi:7.1.0'
    annotationProcessor 'org.spongepowered:spongeapi:7.1.0'

    compileOnly 'com.github.RedNesto:FileInventories:0.3.2'
    compileOnly 'com.github.randombyte-developer:byte-items:v2.3'
    compileOnly 'me.ryanhamshire:griefprevention:1.12.2-4.3.0.662:api'
}

sponge.plugin.id = 'box-o-utils'

shadowJar {
    classifier = null
}

license {
    header = rootProject.file('LICENSE')

    include '**/*.java'

    newLine = false
}

spongestart {
    minecraft '1.12.2'
}

task('sourcesJar', type: Jar) {
    dependsOn 'classes'
    archiveClassifier.set('sources')
    afterEvaluate {
        from sourceSets['main'].allSource
        from project(':common').sourceSets['main'].allSource
    }
}

task('javadocJar', type: Jar) {
    dependsOn 'javadoc'
    dependsOn project(':common').javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
    from project(':common').javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = 'box-o-utils'
            version = version

            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

if (project.file('local.gradle').exists()) {
    // in case anyone wants to have additional tasks
    apply from: 'local.gradle'
}
