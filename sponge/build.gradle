plugins {
    id 'maven-publish'
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'com.qixalite.spongestart2' version '4.0.0'
}

archivesBaseName = 'BoxOUtils'

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compile project(':common')

    compileOnly 'org.spongepowered:spongeapi:7.1.0'
    annotationProcessor 'org.spongepowered:spongeapi:7.1.0'

    compileOnly 'com.github.RedNesto:FileInventories:0.3.2'
    compileOnly 'com.github.randombyte-developer:byte-items:v2.3'
}

sponge.plugin.id = 'box-o-utils'

shadowJar {
    classifier = null
}

license {
    header = rootProject.file('LICENSE')

    include '**/*.java'

    newLine = false
}

spongestart {
    minecraft '1.12.2'
}

task('sourcesJar', type: Jar) {
    dependsOn 'classes'
    archiveClassifier.set('sources')
    afterEvaluate {
        from sourceSets['main'].allSource
        from project(':common').sourceSets['main'].allSource
    }
}

task('javadocJar', type: Jar) {
    dependsOn 'javadoc'
    dependsOn project(':common').javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
    from project(':common').javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = 'box-o-utils'
            version = version

            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
